plugins {
  id 'org.openjfx.javafxplugin' version '0.0.7'
  id 'org.beryx.jlink' version '2.9.4'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}
apply plugin: 'java'
sourceCompatibility = 11
targetCompatibility = 11

apply plugin: 'checkstyle'
checkstyle {
    toolVersion = '8.16'
    configDir = file("$rootProject.projectDir/config/checkstyle")
    reportsDir = file("$project.buildDir/reports/checkstyle")
}

apply plugin: 'application'
application {
    mainClassName = 'zipzop.ui.Main'
}

apply plugin: 'jacoco'
jacoco {
  toolVersion = "0.8.2"
}

repositories {
    jcenter()
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'zipzop/ui/**')
        }))
    }
}

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

test {
  useJUnitPlatform {
      excludeTags 'performance'
  }
  testLogging.showStandardStreams = false
}

dependencies {
    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.4.2'
    )
    testRuntime(
            'org.junit.jupiter:junit-jupiter-engine:5.4.2',
            'org.junit.vintage:junit-vintage-engine:5.4.2'
    )
}

task performanceTests(type: Test) {
    useJUnitPlatform {
        includeTags 'performance'
    }
    shouldRunAfter test
    testLogging.showStandardStreams = true
}

check.dependsOn jacocoTestReport
